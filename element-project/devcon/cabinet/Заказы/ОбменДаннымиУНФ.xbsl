
метод ПроверитьДоступность(Запрос: HttpСервисЗапрос)
    Запрос.Ответ.УстановитьТело("Ok")
;

метод СохранитьОрганизацию(Запрос: HttpСервисЗапрос)
    
    если Запрос.Метод != "POST"
        Запрос.Ответ.УстановитьКодСтатуса(405)
        возврат
    ;
    
    исп КонтекстДоступа.Дополнить(Тип<Организации.Объект>, [Сущность.Право.Чтение, Сущность.Право.Изменение, Сущность.Право.Создание])
    
    исп Поток = Запрос.Тело
    знч Данные = СериализацияJson.ПрочитатьОбъект(Поток, Тип<ЧитаемаяОрганизация>)
    
    знч ОрганизацияСсылка = Организации.ПолучитьСсылку(Данные.Идентификатор)
    пер ОрганизацияОбъект = ОрганизацияСсылка.ЗагрузитьОбъект()
    если ОрганизацияОбъект == Неопределено
        ОрганизацияОбъект = новый Организации.Объект(Данные.Идентификатор)
    ;
    ОрганизацияОбъект.Наименование = Данные.Наименование
    ОрганизацияОбъект.Записать()
    
;

метод СохранитьНоменклатуру(Запрос: HttpСервисЗапрос)
    
    если Запрос.Метод != "POST"
        Запрос.Ответ.УстановитьКодСтатуса(405)
        возврат
    ;
    
    исп КонтекстДоступа.Дополнить(Тип<Номенклатура.Объект>, [Сущность.Право.Чтение, Сущность.Право.Изменение, Сущность.Право.Создание])
    
    исп Поток = Запрос.Тело    
    знч Данные = СериализацияJson.ПрочитатьОбъект(Поток, Тип<ЧитаемыйМассив<ЧитаемаяНоменклатура>>)
    
    для ЭлементДанных из Данные
        знч НоменклатураСсылка = Номенклатура.ПолучитьСсылку(ЭлементДанных.Идентификатор)
        пер НоменклатураОбъект = НоменклатураСсылка.ЗагрузитьОбъект()
        если НоменклатураОбъект == Неопределено
            НоменклатураОбъект = новый Номенклатура.Объект(ЭлементДанных.Идентификатор)
        ;
        НоменклатураОбъект.Наименование = ЭлементДанных.Наименование
        НоменклатураОбъект.Цена = ЭлементДанных.Цена
        НоменклатураОбъект.Записать()
    ;    
    
;

метод СохранитьПредставителя(Запрос: HttpСервисЗапрос)
    
    если Запрос.Метод != "POST"
        Запрос.Ответ.УстановитьКодСтатуса(405)
        возврат
    ;
    
    исп КонтекстДоступа.Привилегированный()
    
    исп Поток = Запрос.Тело    
    знч Данные = СериализацияJson.ПрочитатьОбъект(Поток, Тип<ЧитаемыйПредставитель>)
    
    знч ОрганизацияСсылка = Организации.ПолучитьСсылку(Данные.Организация)
    если не ОрганизацияСуществует(ОрганизацияСсылка)
        Запрос.Ответ.УстановитьКодСтатуса(400)
        Запрос.Ответ.УстановитьТело("Организация с указанным идентификатором не найдена")
        возврат
    ;
    
    знч СписокПользователей = СпискиПользователей.ПолучитьСписокПоУмолчанию()
    
    знч МассивОписаний = СпискиПользователей.ПолучитьПользователей(СписокПользователей.Ид)
    для Описание из МассивОписаний
        если Описание.ЭлектроннаяПочта == Данные.АдресЭлектроннойПочты
            Запрос.Ответ.УстановитьКодСтатуса(400)
            Запрос.Ответ.УстановитьТело("Пользователь с почтой %{Данные.АдресЭлектроннойПочты} уже существует")
            возврат
        ;
    ;
    
    знч Описание = новый ОписаниеПользователя(СписокПользователей.Ид, Данные.Наименование, Данные.АдресЭлектроннойПочты)
        .СТелефоном(Данные.Телефон)
        .СЭлектроннойПочтой(Данные.АдресЭлектроннойПочты)
    ПользователиСервиса.Создать(Описание)
    ПользователиСервиса.СброситьПароль(Описание.Ид, Данные.Пароль)
    знч Пользователь = Пользователи.Подключить(Описание.Ид).Ссылка
    
    знч ПредставительОбъект = новый Представители.Объект(Данные.Идентификатор)
    ПредставительОбъект.Наименование = Данные.Наименование
    ПредставительОбъект.Организация = ОрганизацияСсылка
    ПредставительОбъект.Пользователь = Пользователь
    
    ПредставительОбъект.Записать()
    
;

метод ПолучитьЗаказы(Запрос: HttpСервисЗапрос)
    
    если Запрос.Метод != "GET"
        Запрос.Ответ.УстановитьКодСтатуса(405)
        возврат
    ;
    
    исп КонтекстДоступа.Привилегированный()
    
    знч НомерПолученногоКорреспондентом = новый Число(Запрос.Параметры.ПолучитьПервый("accepted-num") ?? "0")    
    
    знч КодУзлаУНФ = "УНФ"
    
    пер Узел = ОбменУНФ.НайтиПоКоду(КодУзлаУНФ)
    
    если Узел == Неопределено
        знч УзелОбъект = новый ОбменУНФ.Объект()
        УзелОбъект.Код = КодУзлаУНФ
        УзелОбъект.Наименование = "Обмен с УНФ"
        УзелОбъект.Записать()
        Узел = УзелОбъект.Ссылка
        ЗарегистрироватьЗаказы(Узел)
    ;
    
    если НомерПолученногоКорреспондентом > 0
        ОбменУНФ.УдалитьРегистрациюИзмененийПоСообщению(Узел, НомерПолученногоКорреспондентом)
    ;
    
    знч ДанныеДляПередачи = новый Массив<ДанныеЗаказаПокупателя>()
    
    знч НомерСообщения = НомерПолученногоКорреспондентом + 1
    
    область
    
        исп Транзакции.Начать()
        
        знч УзелОбъект = Узел.ЗагрузитьОбъект(Истина)
        УзелОбъект.НомерОтправленного = НомерСообщения
        УзелОбъект.Записать()
        
        исп Выборка = ОбменУНФ.ВыбратьИзмененияВСообщение(Узел, НомерСообщения)
        
        для СвойстваЗаказа из Выборка
            если СвойстваЗаказа это ЗаказПокупателя.Объект
                ЗаписатьИзмененияЗаказаПокупателя(ДанныеДляПередачи, СвойстваЗаказа как ЗаказПокупателя.Объект)
            ;
        ;
    
    ;
    
    исп ПотокЗаписи = Запрос.Ответ.ОткрытьПотокЗаписиТела()
    Запрос.Ответ.Заголовки.Установить("Content-Type", "application/json")
    СериализацияJson.ЗаписатьОбъект(ПотокЗаписи, ДанныеДляПередачи)
    
;

метод ЗаписатьИзмененияЗаказаПокупателя(ДанныеДляПередачи: Массив<ДанныеЗаказаПокупателя>, СвойстваЗаказа: ЗаказПокупателя.Объект)
    
    знч СписокНоменклатуры = новый Массив<СтрокаЗаказаПокупателя>()
    
    для Строка из СвойстваЗаказа.СписокНоменклатуры
        знч ДанныеСтроки = новый СтрокаЗаказаПокупателя(
            Номенклатура = Строка.Номенклатура.Ид,
            Количество = Строка.Количество,
            Цена = Строка.Цена,
            Сумма = Строка.Сумма
        )
        СписокНоменклатуры.Добавить(ДанныеСтроки)
    ;
    
    знч ДанныеОбъекта = новый ДанныеЗаказаПокупателя(
        Идентификатор = СвойстваЗаказа.Ссылка.Ид,
        Организация = СвойстваЗаказа.Организация.Ид,
        Представитель = СвойстваЗаказа.Ответственный.Ид,
        Комментарий = СвойстваЗаказа.Комментарий,
        СписокНоменклатуры = СписокНоменклатуры
    )
    
    ДанныеДляПередачи.Добавить(ДанныеОбъекта)
    
;

метод ОрганизацияСуществует(Ссылка: Организации.Ссылка): Булево
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Организации.Ссылка КАК Ссылка
        ИЗ
            Организации КАК Организации
        ГДЕ
            Организации.Ссылка == %Ссылка
    }
    
    возврат не Запрос.Выполнить().Пусто()
    
;

метод ЗарегистрироватьЗаказы(Узел: ОбменУНФ.Ссылка)
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            ЗаказПокупателя.Ссылка КАК Ссылка
        ИЗ
            ЗаказПокупателя КАК ЗаказПокупателя
    }
    
    знч РезультатЗапроса = Запрос.Выполнить()
    
    если РезультатЗапроса.Пусто()
        возврат
    ;
    
    ОбменУНФ.ЗарегистрироватьИзменения(Узел, РезультатЗапроса.Преобразовать(Строка -> Строка.Ссылка))
    
;

@Локально
структура ЧитаемаяОрганизация
    пер Идентификатор: Ууид
    пер Наименование: Строка
;

@Локально
структура ЧитаемаяНоменклатура
    пер Идентификатор: Ууид
    пер Наименование: Строка
    пер Цена: Число    
;

@Локально
структура ЧитаемыйПредставитель
    пер Идентификатор: Ууид
    пер Организация: Ууид
    пер Наименование: Строка
    пер АдресЭлектроннойПочты: Строка
    пер Пароль: Строка
    пер Телефон: Строка
;

@Локально
структура СтрокаЗаказаПокупателя
    пер Номенклатура: Ууид
    пер Количество: Число
    пер Цена: Число
    пер Сумма: Число
;

@Локально
структура ДанныеЗаказаПокупателя
    пер Идентификатор: Ууид
    пер Организация: Ууид
    пер Представитель: Ууид
    пер Комментарий: Строка
    пер СписокНоменклатуры: Массив<СтрокаЗаказаПокупателя>
;
